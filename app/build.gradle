apply plugin: 'com.android.application'

configurations.all {
    resolutionStrategy.force 'com.android.support:support-annotations:23.1.0'
}

def keystoreFilePath = ''
def keystorePSW = ''
def keystoreAlias = ''
def keystoreAliasPSW = ''
def keyFile = file('s.keystore.temp')

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
keystoreFilePath = properties.getProperty("keystore.path")

if (keystoreFilePath) {
    keystorePSW = properties.getProperty("keystore.password")
    keystoreAlias = properties.getProperty("keystore.alias")
    keystoreAliasPSW = properties.getProperty("keystore.alias_password")
    keyFile = file(keystoreFilePath)
}

android {
    signingConfigs {
        debug {
            keyAlias keystoreAlias
            keyPassword keystoreAliasPSW
            storeFile keyFile
            storePassword keystorePSW
        }

        release {
            keyAlias keystoreAlias
            keyPassword keystoreAliasPSW
            storeFile keyFile
            storePassword keystorePSW
        }
    }

    compileSdkVersion 25
    buildToolsVersion '25.0.2'
    defaultConfig {
        applicationId "com.sx.quality.activity"

        ndk {
            // 选择要添加的对应cpu类型的.so库。
            abiFilters 'armeabi', 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
            // 还可以添加 'x86', 'x86_64', 'mips', 'mips64'
        }

        manifestPlaceholders = [
                JPUSH_PKGNAME: applicationId,
                JPUSH_APPKEY : "427374ed061f0645b378569d", //JPush上注册的包名对应的appkey.
                JPUSH_CHANNEL: "developer-default", // 暂时填写默认值即可.
        ]

        minSdkVersion 16
        targetSdkVersion 25
        versionCode 2
        versionName "3.8.1"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            minifyEnabled false
        }

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (keyFile.exists()) {
                signingConfig signingConfigs.release
            }
            debuggable false
        }
    }

    // 配置打包文件名
    android.applicationVariants.all {
        variant ->
            variant.outputs.each {
                output -> output.outputFile = new File(output.outputFile.parent, "Quality_v" + defaultConfig.versionName + "_" + buildType.name + ".apk");
            }
    }

    lintOptions {
        abortOnError false
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
        }
    }
}

dependencies {
    provided fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    // 系统jar
    compile 'com.android.support:appcompat-v7:25.+'
    compile 'com.android.support:recyclerview-v7:25.+'
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    // 引入百度定位所需jar包
    compile files('libs/BaiduLBS_Android.jar')
    // 百分比布局所需jar包
    compile 'com.zhy:percent-support-extends:1.1.1'
    // 注解所需jar包
    compile 'org.xutils:xutils:3.5.0'
    // 日志打印所需jar包
    compile 'com.orhanobut:logger:2.1.1'
    // 图片加载所需jar包
    compile 'com.github.bumptech.glide:glide:4.2.0'
    // 网络请求所需jar包
    compile 'com.squareup.okhttp3:okhttp:3.9.0'
    // json解析所需jar包
    compile 'com.google.code.gson:gson:2.6.2'
    // 多文件上传所需jar包
    compile 'com.zhy:okhttputils:2.6.2'
    // 网络请求加载dialog
    compile 'com.zyao89:zloading:1.0.8'
    // 本地数据库
    compile 'org.litepal.android:core:1.6.0'
    // 图片展示所需jar包
    compile 'com.github.chrisbanes:PhotoView:2.0.0'
    // 极光推送所需jar包
    compile 'cn.jiguang.sdk:jpush:3.0.9'
    compile 'cn.jiguang.sdk:jcore:1.1.7'
    // 轮播图
    compile 'com.youth.banner:banner:1.4.10'
    // 跑马灯文字
    compile 'com.dalong:marqueeview:1.0.1'
    // 日期...工具类
    compile 'cn.hutool:hutool-all:4.0.9'
    // 图片选择器
    compile 'com.yuyh.imgsel:library:2.0.2'
    // 水印文字生成
    compile 'com.vinaygaba:rubberstamp:1.0.0'
    // 柱状图
    compile 'com.github.PhilJay:MPAndroidChart:v3.0.3'
    // 时间选择器
    compile('com.github.gzu-liyujiang.AndroidPicker:WheelPicker:1.5.5') {
        exclude group: 'com.android.support'
    }
    // 搜索框
    compile 'com.github.mancj:MaterialSearchBar:0.7.6'
    // 时间轴---瀑布流
    compile 'com.github.vipulasri:timelineview:1.0.6'
    compile 'com.vivian.widgets:TimeLineItemDecoration:1.3'
    testCompile 'junit:junit:4.12'
}
